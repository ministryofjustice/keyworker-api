import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.owasp.dependencycheck' version '5.3.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

configurations {
    agentDeps
    agentDeps.transitive = false
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

ext {
    ext {
        drivers = [
                'chromeHeadless'
        ]
    }
}

dependencyCheck {
    failBuildOnCVSS = 5
    suppressionFiles = ['dependency-check-suppress-spring.xml']
    format = 'ALL'
    analyzers {
        assemblyEnabled = false
    }
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

group = 'uk.gov.justice.digital.hmpps'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

jar {
    baseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': rootProject.name, 'Implementation-Version': version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = version
            group = group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

configurations {
    compile.exclude  module: "tomcat-jdbc"
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE'
    implementation 'org.springframework.security:spring-security-jwt:1.1.0.RELEASE'

    implementation 'org.apache.camel:camel-spring-boot:2.24.3'
    implementation 'org.apache.camel:camel-quartz2:2.25.0'

    implementation 'org.springframework:spring-jms'
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.hibernate:hibernate-java8'

    implementation 'net.logstash.logback:logstash-logback-encoder:6.3'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.0-BETA'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.0-BETA'
    agentDeps 'com.microsoft.azure:applicationinsights-agent:2.6.0-BETA'
    compile configurations.agentDeps

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'javax.transaction:javax.transaction-api:1.3'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    implementation 'io.jsonwebtoken:jjwt:0.9.1'

    implementation 'net.sf.ehcache:ehcache:2.10.6'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-text:1.8'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.2'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.3'

    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.2.0'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'com.google.code.gson:gson:2.8.6'

    compileOnly 'org.projectlombok:lombok:1.18.12'

    runtime 'org.hsqldb:hsqldb:2.5.0'
    runtime 'org.flywaydb:flyway-core:6.2.4'
    runtime 'org.postgresql:postgresql:42.2.10'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.9'
    testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
    testCompile('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: "org.codehaus.groovy"
    }

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testImplementation 'org.gebish:geb-core:3.3'
    testImplementation 'org.gebish:geb-spock:3.3'
    testImplementation 'org.seleniumhq.selenium:selenium-support:3.141.59'
    testImplementation 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59'
    testImplementation 'org.seleniumhq.selenium:selenium-firefox-driver:3.141.59'
    testImplementation 'io.github.http-builder-ng:http-builder-ng-apache:1.0.4'

    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.26.2'
    testImplementation 'com.github.tomjankes:wiremock-groovy:0.2.0'
    testImplementation 'org.apache.camel:camel-test-spring:2.24.3'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'org.testcontainers:localstack:1.12.5'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.14.0'
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

assemble.dependsOn copyAgent

test {
    useJUnitPlatform()
}
