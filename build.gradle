import java.text.SimpleDateFormat

buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

group 'uk.gov.justice.digital.hmpps'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

def applicationMainClass = 'uk.gov.justice.digital.hmpps.keyworker.KeyworkerApplication'
def dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss")
def applicationName = 'keyworker-service'

jar {
    baseName = applicationName
    manifest {
        attributes 'Implementation-Title': applicationName,
                'Implementation-Version': System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
    }
}

springBoot {
    mainClass = applicationMainClass

    buildInfo {
        // Generate extra build info.
        additionalProperties = [
                by                   : System.properties['user.name'],
                operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                continuousIntegration: System.getenv('CI') ? true : false,
                machine              : InetAddress.localHost.hostName,
                time                 : dateFormat.format(new Date()),
                name                 : applicationName,
                version              : System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
        ]
    }
    executable = true
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }

    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.hibernate:hibernate-java8")

    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.security.oauth:spring-security-oauth2")

    compile("org.springframework.security:spring-security-jwt")
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'

    compile("io.springfox:springfox-swagger2:2.8.0")
    compile("io.springfox:springfox-swagger-ui:2.8.0")

    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.8.3'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '4.8'
    compile group: 'com.pauldijou', name: 'jwt-core_2.11', version: '0.15.0'

    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.0.2'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    compile 'org.projectlombok:lombok:1.16.18'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.14.0'
}